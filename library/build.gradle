apply plugin: 'com.android.library'
apply plugin: 'maven'

group = 'com.nobrain.android.permissions'
version = '1.0.0'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

artifacts {

    archives sourcesJar
    archives javadocJar
}


dependencies {
    testCompile 'junit:junit:4.12'
}

uploadArchives {
    repositories {
        mavenDeployer {

            configuration = configurations.archives

            repository(url: uri('./repo'))
            pom.project {
                name 'Android Permission Checker'
                description "It's for checing Android Permissions on Runtime"
                url 'https://github.com/ZeroBrain/AndroidPermissions'
                inceptionYear '2015'
                packaging 'jar'

                scm {
                    url 'https://github.com/ZeroBrain/AndroidPermissions'
                    connection 'scm:git:git://github.com/ZeroBrain/AndroidPermissions.git'
                    developerConnection 'scm:git:ssh://git@github.com/ZeroBrain/AndroidPermissions.git'
                    tag 'HEAD'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                organization {
                    name 'Nobrain'
                    url 'https://medium.com/@jsuch2362'
                }

                developers {
                    developer {
                        id 'jsuch2362'
                        name 'Steve SeongUg Jung'
                        email 'jsuch2362@gmail.com'
                    }
                }
            }

        }
    }
}